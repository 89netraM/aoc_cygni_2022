📦 files 🏠

🏁 🍇
	🔧 🍺🔡 🍺📇 🐇📄 🔤./input.txt🔤 ❗ ❗ ❗ ➡ input
	🔫 input 🔤❌n🔤 ❗ ➡ lines

	🌳 🐇💻 🔤part🔤 ❗ ➡ part
	↪ ❎ part 🙌 🤷‍♀️ ❗ 🤝 🍺part 🙌 🔤part2🔤 🍇
		0 ➡ 🖍🆕prios
		🔂 i 🆕 ⏩ 0 📏 lines ❓ 3 ❗ 🍇
			💧 🐇🎄 🐽 lines i ❗ ❗ ➡ first
			💧 🐇🎄 🐽 lines i ➕ 1 ❗ ❗ ➡ second
			💧 🐇🎄 🐽 lines i ➕ 2 ❗ ❗ ➡ third
			🌠 🐇🎄 🌠 🐇🎄 first second ❗ third ❗ ➡ common
			↪ 📏 common ❓ ▶ 0 🍇
				prios ⬅➕ 🅰 🐇🎄 🐽 common 0 ❗ ❓
			🍉
		🍉
		😀 🔡 prios ❗ ❗
	🍉
	🙅‍♀️ 🍇
		0 ➡ 🖍🆕prios
		🔂 line lines 🍇
			📐 line ❗ ➗ 2 ➡ half
			💧 🐇🎄 🔪 line 0 half ❗ ❗ ➡ first
			💧 🐇🎄 🔪 line half half ❗ ❗ ➡ second
			🌠 🐇🎄 first second ❗ ➡ common
			↪ 📏 common ❓ ▶ 0 🍇
				prios ⬅➕ 🅰 🐇🎄 🐽 common 0 ❗ ❓
			🍉
		🍉
		😀 🔡 prios ❗ ❗
	🍉
🍉

🐇 🎄 🍇
	🐇❗ 💧 string🔡 ➡ 🍨🐚💧🍆 🍇
		📐 string ❗ ➡ length
		🆕 🍨🐚💧🍆▶🐴 length ❗ ➡ 🖍🆕bytes

		☣ 🍇
			🧠 string ❗ ➡ string_memory
			🔂 i 🆕 ⏩ 0 length ❗ 🍇
				🐻 bytes 🐽🐚💧🍆 string_memory i ❗ ❗
			🍉
		🍉

		↩ bytes
	🍉

	🐇❗ 🌠 a🍨🐚💧🍆 b🍨🐚💧🍆 ➡ 🍨🐚💧🍆 🍇
		🆕 🍯🐚💧🍆 ❗ ➡ 🖍🆕common

		🔂 item a 🍇
			↪ 🐦 b item ❓ 🍇
				item ➡ 🐽 common 🔡 item ❗ ❗
			🍉
		🍉

		🆕 🍨🐚💧🍆▶🐴 📏 common ❓ ❗ ➡ 🖍🆕items
		🔂 key 🐙 common ❗ 🍇
			🐻 items 🍺🐽 common key ❗ ❗
		🍉
		↩ items
	🍉

	🐇❓ 🅰 byte💧 ➡ 🔢 🍇
		↪ byte ◀🙌 90 🍇
			↩ 🔢 byte ❗ ➖ 38
		🍉
		🙅‍♀️ 🍇
			↩ 🔢 byte ❗ ➖ 96
		🍉
	🍉
🍉
